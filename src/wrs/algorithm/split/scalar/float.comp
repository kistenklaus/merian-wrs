#version 460

#extension GL_KHR_shader_subgroup_arithmetic : enable
#extension GL_KHR_memory_scope_semantics : enable
#extension GL_EXT_scalar_block_layout    : enable
#extension GL_KHR_shader_subgroup_vote : enable
#pragma use_vulkan_memory_model

// For some reason precise is required to avoid numerical instabilities
// against the reference for testing.
#define monoid precise float

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

struct Split {
    uint i;
    uint j;
    monoid spill;
};

layout(set = 0, binding = 0, std140) readonly buffer in_partitionPrefixSums {
    uint heavyCount;
    monoid heavyLight[];
} partitionPrefix;

layout(set = 0, binding = 1, std140) readonly buffer in_weightAverage {
    monoid mean;
} meta;

layout(set = 0, binding = 2, std140) writeonly buffer out_splits {
    Split splits[];
};

layout(push_constant) uniform push_constants {
    uint K;
    uint N;
} pc;

uint heavyCount;
uint lightCount;
uint lightPrefixFirst;

bool heavyPrefixAt(uint i, out monoid heavyPrefix) {
    if (i < heavyCount) {
        heavyPrefix = partitionPrefix.heavyLight[i];
        return true;
    } else {
        return false;
    }
}

bool lightPrefixAt(uint i, out monoid lightPrefix) {
    if (i < lightCount) {
        lightPrefix = partitionPrefix.heavyLight[lightPrefixFirst - i];
        return true;
    } else {
        return false;
    }
}

// computes the ceiling division of a * b by c
// assumes that a > c > b
uint ceilMulDiv(uint a, uint b, uint c) {
  uint div = a / c;
  uint rem = a % c;
  uint safe_part = div * b;
  uint rem_part = (rem * b + c - 1) / c;
  return safe_part + rem_part;
}

void main(void) {
    const uint N = pc.N;
    const uint K = pc.K;
    const monoid mean = meta.mean;
    heavyCount = partitionPrefix.heavyCount;
    lightCount = N - heavyCount;
    lightPrefixFirst = N - 1;

    const uint k = gl_WorkGroupID.x + 1;
    const uint n = ceilMulDiv(N, k, K);

    if (gl_WorkGroupID.x == 0) {
        splits[K - 1].i = lightCount;
        splits[K - 1].j = heavyCount;
        splits[K - 1].spill = 0;
    }

    uint a = 0;
    uint b = min(n, heavyCount);
    const monoid target = mean * n;

    uint j, i;
    monoid sigma2;

    while (true) {
        j = (a + b) / 2;
        i = min(n - j, lightCount);

        if (a > b) {
            sigma2 = partitionPrefix.heavyLight[lightPrefixFirst - i]
                    + partitionPrefix.heavyLight[j + 1];
            break;
        }

        monoid heavy = j == 0 ? 0 : partitionPrefix.heavyLight[j - 1];
        monoid light = i == 0 ? 0 : partitionPrefix.heavyLight[lightPrefixFirst - i + 1];

        const monoid sigma = light + heavy;
        if (sigma <= target) {
            monoid heavy2 = partitionPrefix.heavyLight[j];
            sigma2 = light + heavy2;
            if (sigma2 > target) {
                break;
            } else {
                a = j + 1;
            }
        } else {
            b = j - 1;
        }
    }
    splits[gl_WorkGroupID.x].i = i;
    splits[gl_WorkGroupID.x].j = j;
    splits[gl_WorkGroupID.x].spill = sigma2 - target;
}
