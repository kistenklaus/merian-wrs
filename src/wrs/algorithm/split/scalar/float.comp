#version 460

#extension GL_KHR_shader_subgroup_arithmetic : enable
#extension GL_KHR_memory_scope_semantics : enable
#extension GL_EXT_scalar_block_layout    : enable
#extension GL_KHR_shader_subgroup_vote : enable
#pragma use_vulkan_memory_model

layout(local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

struct Split {
    uint i;
    uint j;
    float spill;
};

layout(set = 0, binding = 0, std430) readonly buffer in_partitionPrefixSums {
    uint heavyCount;
    float heavyLight[];
} partitionPrefix;

layout(set = 0, binding = 1, std430) readonly buffer in_weightAverage {
    float mean;
} meta;

layout(set = 0, binding = 2, std430) writeonly buffer out_splits {
    Split splits[];
};

layout(push_constant) uniform push_constants {
    uint K;
    uint N;
} pc;

uint heavyCount;
uint lightCount;
uint lightPrefixFirst;

bool heavyPrefixAt(uint i, out float heavyPrefix) {
    if (i < heavyCount) {
        heavyPrefix = partitionPrefix.heavyLight[i];
        return true;
    } else {
        return false;
    }
}

bool lightPrefixAt(uint i, out float lightPrefix) {
    if (i < lightCount) {
        lightPrefix = partitionPrefix.heavyLight[lightPrefixFirst - i];
        return true;
    } else {
        return false;
    }
}

// computes the ceiling division of a * b by c
// assumes that a > c > b
uint ceilMulDiv(uint a, uint b, uint c) {
  uint div = a / c;
  uint rem = a % c;
  uint safe_part = div * b;
  uint rem_part = (rem * b + c - 1) / c;
  return safe_part + rem_part;
}

void computeSplit(uint k, uint N, uint K, float mean, uint heavyCount, uint lightCount, uint lightPrefixFirst) {
    const uint n = ceilMulDiv(N, k, K);

    uint a = 0;
    uint b = min(n, heavyCount);
    const float target = mean * n;

    uint j = 0, i = 0;
    float sigma2 = 0;

    while (true) {
        j = (a + b) / 2;
        i = min(n - j, lightCount);


        if (a > b) {
            sigma2 = partitionPrefix.heavyLight[lightPrefixFirst - i]
                    + partitionPrefix.heavyLight[j + 1];
            break;
        }

        const float heavy = j == 0 ? 0 : partitionPrefix.heavyLight[j - 1];
        const float light = i == 0 ? 0 : partitionPrefix.heavyLight[lightPrefixFirst - i + 1];

        const float sigma = light + heavy;

        if (sigma <= target) {
            float heavy2 = partitionPrefix.heavyLight[j];
            sigma2 = light + heavy2;
            if (sigma2 > target) {
                break;
            } else {
                a = j + 1;
            }
        } else {
            b = j - 1;
        }
    }
    splits[k - 1].i = i;
    splits[k - 1].j = j;
    splits[k - 1].spill = sigma2 - target;
}

void main(void) {
    const uint N = pc.N;
    const uint K = pc.K;
    const float mean = meta.mean;
    heavyCount = partitionPrefix.heavyCount;
    lightCount = N - heavyCount;
    lightPrefixFirst = N - 1;

    const uint k = gl_GlobalInvocationID.x + 1;

    if (k >= K) {
        return; // Ensure k does not exceed or equal the number of splits (K)
    }

    if (gl_GlobalInvocationID.x == 0) {
        splits[K - 1].i = lightCount;
        splits[K - 1].j = heavyCount;
        splits[K - 1].spill = 0;
    }

    computeSplit(k, N, K, mean, heavyCount, lightCount, lightPrefixFirst);
}
