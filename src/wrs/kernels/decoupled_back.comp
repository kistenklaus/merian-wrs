#version 450

#extension GL_KHR_shader_subgroup_arithmetic : enable
#extension GL_KHR_memory_scope_semantics : enable
// #extension GL_EXT_shader_atomic_float : enable
#extension GL_KHR_shader_subgroup_vote : enable
#extension GL_KHR_shader_subgroup_ballot : enable
#pragma use_vulkan_memory_model

#define monoid float

layout(constant_id = 0) const uint GROUP_SIZE = 1;
layout(constant_id = 1) const uint SUBGROUP_SIZE = 1;
layout(constant_id = 2) const uint ROWS = 1;
const uint PARTITION_SIZE = GROUP_SIZE * ROWS;
const uint SUBGROUP_COUNT = (GROUP_SIZE + SUBGROUP_SIZE - 1) / SUBGROUP_SIZE;
layout(local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

#define STATE_NO_AGGREGATE 0u
#define STATE_AGGREGATE 1u
#define STATE_PREFIX 2u
#define STATE_DONT_CARE 3u

struct PartitionDescriptor {
    monoid aggregate;
    monoid prefix;
    uint state;
};

layout(set = 0, binding = 0) readonly buffer in_values {
    monoid values[];
};

layout(set = 0, binding = 1) writeonly buffer out_inclusive_prefix_sum {
    monoid prefix_sum[];
};

layout(set = 0, binding = 2) volatile buffer parition_descriptors {
    uint atomicWorkIDCounter;
    PartitionDescriptor partitions[];
};

#define LOOK_BACK_STATE_SPIN 1u
#define LOOK_BACK_STATE_DONE 0u

shared uint lookBackState;
shared uint groupID;
shared monoid partPrefix;
shared monoid scratch[GROUP_SIZE];

#define ATOMIC_LOOK_BACK_STATE
#define ATOMIC_PARTITION_STATE

#define PARALLEL_LOOK_BACK

void main(void) {
    monoid local[ROWS];

    const uint invocID = gl_LocalInvocationID.x;
    const uint subInvocID = gl_SubgroupInvocationID.x;
    const uint subgroupID = gl_SubgroupID.x;
    const uint subgroupCount = gl_NumSubgroups.x;
    const uint subgroupSize = gl_SubgroupSize.x;
    const uint lookBackMaxStepSize = min(subgroupSize, 32);
    if (invocID == 0) {
        groupID = atomicAdd(atomicWorkIDCounter, 1);
    }
    controlBarrier(gl_ScopeWorkgroup, gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsAcquireRelease);

    uint partID = groupID;
    uint ix = partID * PARTITION_SIZE + invocID * ROWS;
    // uint itemID = partID * PARTITION_SIZE + invocID * ROWS;
    bool seqInvoc = invocID == gl_WorkGroupSize.x - 1;
    bool seqSubgroup = subgroupID == subgroupCount - 1;

    // 1. Step calculate group aggregate
    local[0] = values[ix];
    for (uint i = 1; i < ROWS; ++i) {
        local[i] = local[i - 1] + values[ix + i];
    }
    monoid agg = local[ROWS - 1];

    scratch[invocID] = agg;

    for (uint shift = 1; shift <= (GROUP_SIZE >> 1) ; shift <<= 1) {
        barrier();
        // controlBarrier(gl_ScopeWorkgroup, gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsAcquireRelease);
        if (invocID >= shift) {
            monoid other = scratch[invocID - shift];
            agg = agg + other;
        }
        barrier();
        // controlBarrier(gl_ScopeWorkgroup, gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsAcquireRelease);
        scratch[invocID] = agg;
    }

    // 2. Publish Aggregate and state
    // 2.1 Publish aggregate
    if (seqInvoc) {
        partitions[partID].aggregate = agg;
        if (partID == 0) {
            partitions[partID].prefix = agg;
        }
    }
    // barrier();
    // controlBarrier(gl_ScopeWorkgroup, gl_ScopeDevice, gl_StorageSemanticsBuffer, gl_SemanticsAcquireRelease);

    if (seqInvoc) {
        uint state = STATE_AGGREGATE;
        if (partID == 0) {
          state = STATE_PREFIX;
        }
        atomicStore(partitions[partID].state, state, gl_ScopeDevice,
            gl_StorageSemanticsBuffer, gl_SemanticsRelease);
    }

    // controlBarrier(gl_ScopeWorkgroup, gl_ScopeDevice, gl_StorageSemanticsBuffer, gl_SemanticsAcquireRelease);

    // 3. Decoupled Look back
    monoid exclusive_prefix = 0;
    if (partID != 0) {
        int previousPartIdx = int(partID) - 1;
        uint s = STATE_NO_AGGREGATE;
        uint cond = LOOK_BACK_STATE_SPIN;
        #ifdef ATOMIC_LOOK_BACK_STATE
        if (seqInvoc) {
            atomicStore(lookBackState, cond,
                gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsRelease);
        }
        #else
        lookBackState = cond;
        #endif
        // controlBarrier(gl_ScopeWorkgroup, gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsAcquireRelease);
        uint lookBackPerfCounter = 0;
        monoid exclusive = 0;
        while (cond == LOOK_BACK_STATE_SPIN) {
            lookBackPerfCounter++;

            if (seqSubgroup) {
                bool participant = subInvocID < lookBackMaxStepSize && subInvocID <= previousPartIdx;
                if (participant) {
                    uint subgroupInvocPartIdx = previousPartIdx - subInvocID;
                    #ifdef ATOMIC_PARTITION_STATE
                    s = atomicLoad(partitions[subgroupInvocPartIdx].state, gl_ScopeQueueFamily,
                            gl_StorageSemanticsBuffer, gl_SemanticsAcquire | gl_SemanticsMakeVisible);
                    #else
                    s = partitions[subgroupInvocPartIdx].state;
                    #endif
                } else {
                    s = STATE_DONT_CARE;
                }
                subgroupBarrier();
                bool hasNoAggregate = s == STATE_NO_AGGREGATE;
                bool hasPrefix = s == STATE_PREFIX;
                if (subgroupAny(hasNoAggregate)) {
                    // lookBackState = LOOK_BACK_STATE_SPIN
                } else if (subgroupAny(hasPrefix)) {
                    // uint t = min(lookBackPerfCounter, 9);
                    // item += 1000 * t;
                    uvec4 prefixBallot = subgroupBallot(hasPrefix);
                    uint closestPrefix = subgroupBallotFindLSB(prefixBallot);
                    if (subInvocID <= closestPrefix) {
                        monoid v;
                        uint idx = previousPartIdx - subInvocID;
                        if (subInvocID == closestPrefix) {
                            v = partitions[idx].prefix;
                        } else {
                            v = partitions[idx].aggregate;
                        }
                        monoid partition_prefix = subgroupAdd(v);
                        if (subgroupElect()) {
                            partPrefix = partition_prefix + exclusive;
                        }
                    }

                    // sum up everything after the closest part with a prefix
                    if (subgroupElect()) {
                        #ifdef ATOMIC_LOOK_BACK_STATE
                        atomicStore(lookBackState, LOOK_BACK_STATE_DONE,
                            gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsRelease);
                        #else
                        lookBackState = LOOK_BACK_STATE_DONE;
                        #endif
                    }
                } else {
                    uint stepSize = min(previousPartIdx, lookBackMaxStepSize);
                    // uint t = min(lookBackPerfCounter, 9);
                    // item += 10000 * t + 10 * stepSize;

                    if (participant) {
                        monoid v = partitions[previousPartIdx - subInvocID].aggregate;
                        monoid partition_aggregate = subgroupAdd(v);
                        // uint partition_aggregate = stepSize;
                        exclusive += partition_aggregate;
                    }
                    previousPartIdx -= int(stepSize);
                    // Compute running prefix!
                    if (previousPartIdx < 0) {
                        if (subgroupElect()) {
                            partPrefix = exclusive;
                        }
                        if (subgroupElect()) {
                            #ifdef ATOMIC_LOOK_BACK_STATE
                            atomicStore(lookBackState, LOOK_BACK_STATE_DONE,
                                gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsRelease);
                            #else
                            lookBackState = LOOK_BACK_STATE_DONE;
                            #endif
                        }
                    }
                }
            }
            controlBarrier(gl_ScopeWorkgroup, gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsAcquireRelease);
            #ifdef ATOMIC_LOOK_BACK_STATE
            cond = atomicLoad(lookBackState, gl_ScopeWorkgroup, gl_StorageSemanticsShared, gl_SemanticsAcquire);
            #else
            cond = lookBackState;
            #endif
        }
        exclusive_prefix = partPrefix;
        if (seqInvoc) {
            partitions[partID].prefix = exclusive_prefix + agg;
        }

        if (seqInvoc) {
            atomicStore(partitions[partID].state, STATE_PREFIX, gl_ScopeQueueFamily,
                gl_StorageSemanticsBuffer, gl_SemanticsRelease);
        }
    }
    // Compute partition inclusive prefix sum
    monoid row = exclusive_prefix;
    if (invocID > 0) {
        monoid other = scratch[invocID - 1];
        row = row + other;
    }
    for (uint i = 0; i < ROWS; ++i) {
        monoid m = row + local[i];
        prefix_sum[ix + i] = m;
    }
}
